<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN" "http://www.w3.org/TR/REC-html40/loose.dtd">
<html>

<head>
<title>Programming with AOLserver</title>
<style type="text/css">
BODY {
  background-color: white;
  color: black
}
</style>
</head>

<body>

<h2>Programming with AOLserver</h2>

by <a href="mailto:michael@arsdigita.com">Michael Yoon</a>, <a href="mailto:jsalz@mit.edu">Jon Salz</a> and 
<a href="http://www.pinds.com/lars">Lars Pind</a>.

<br>

part of <a href="">the ACS Engineering Manual</a>

<hr>

<h3>The <code>global</code> command</h3>

When using AOLserver, remember that there are effectively <em>two</em>
types of global namespace, not one:


<ol>

<li><strong><em>Server</em>-global</strong>: As you'd expect, there is
only one server-global namespace per server, and variables set within
it can be accessed by any Tcl code running subsequently, in any of the
server's threads. To set/get server-global variables, use AOLserver
3's <a
href="http://www.aolserver.com/doc/3.0/nsv.txt"><code>nsv</code>
API</a> (which supersedes <code>ns_share</code> from the pre-3.0 API).

<p>

<li><strong><em>Script</em>-global</strong>: Each Tcl script (ADP, Tcl
page, registered proc, filter, etc.) executing within an AOLserver
thread has its own global namespace. Any variable set in the top level
of a script is, by definition, script-global, meaning that it is
accessible only by subsequent code in the same script and only for the
duration of the current script execution.

</ol>

The Tcl built-in command <a
href="http://aolserver.com/doc/tcl8.3/TclCmd/global.htm"><code>global</code></a>
accesses script-global, <em>not</em> server-global, variables from
within a procedure. This distinction is important to understand in
order to use <code>global</code> correctly when programming AOLserver.

<p>

Also, AOLserver purges all script-global variables in a thread (i.e.,
Tcl interpreter) between HTTP requests. If it didn't, that would
affect (and complicate) our use of script-global variables
dramatically, which would then be better described as
<em>thread</em>-global variables. Given AOLserver's behaviour,
however, "script-global" is a more appropriate term.

<h3>Threads and Scheduled Procedures</h3>

<code>ns_schedule_proc</code> and <code>ad_schedule_proc</code> each take
a <code>-thread</code> flag to cause a scheduled procedure to run
asychronously, in its own thread. It almost always seems like a good idea
to specify this switch, but there's a problem.

<p>It turns out that whenever a task scheduled with
<code>ns_schedule_proc -thread</code> or <code>ad_schedule_proc -thread t</code>
is run, AOLserver creates a brand new thread and a brand new
interpreter, and reinitializes the procedure table
(essentially, loads all procedures that were
created during server initialization into the new interpreter). This happens
<i>every time</i> the task is executed - and it
is a very expensive process that should not be taken lightly!

<p><b>The moral:</b> if you have a lightweight scheduled procedure which runs
frequently, <b>don't use the <code>-thread</code> switch</b>.

<p>

<blockquote><em>Note also that thread is initialized with a copy of what was installed during
server startup, so if the procedure table have changed since startup (e.g. using the
<a href="/doc/core-arch-guide/apm">APM</a> watch facility), that will not 
be reflected in the scheduled thread.</em></blockquote>

<h3>Using <tt>return</tt></h3>

The <tt>return</tt> command in Tcl returns control to the caller
procedure. This definition allows nested procedures to work
properly. However, this definition also means that nested procedures
cannot use <tt>return</tt> to end an entire thread. This situation is
most common in exception conditions that can be triggered from inside
a procedure e.g., a permission denied exception. At this point, the
procedure that detects invalid permission wants to write an error
message to the user, and completely abort execution of the caller
thread. <tt>return</tt> doesn't work, because the procedure may be
nested several levels deep. We therefore use <a
href="/api-doc/proc-view?proc=ad%5fscript%5fabort"><tt>ad_script_abort</tt></a>
to abort the remainder of the thread. Note that using <tt>return</tt>
instead of <tt>ad_script_abort</tt> may raise some security issues: an
attacker could call a page that performed some DML statement, pass
in some arguments, and get a permission denied error -- but the DML
statement would still be executed because the thread was not
stopped. Note that <tt>return -code return</tt> can be used in
circumstances where the procedure will only be called from two levels
deep.


<h3>Returning More Than One Value From a Function</h3>

Many functions have a single return value. For instance, <a
href="/api-doc/proc-view?proc=empty%5fstring%5fp"><code>empty_string_p</code></a>
returns a number: 1 or 0. Other functions need to return a composite
value. For instance, consider a function that looks up a user's name
and email address, given an ID. One way to implement this is to return
a three-element list and document that the first element contains the
name, and the second contains the email address. The problem with this
technique is that, because Tcl does not support constants, calling
procedures that returns lists in this way necessitates the use of
magic numbers, e.g.:

<blockquote><pre>
set user_info [ad_get_user_info $user_id]
set first_name [lindex $user_info 0]
set email [lindex $user_info 1]
</pre></blockquote>

<p>

AOLserver/Tcl generally has three mechanisms that we like, for
returning more than one value from a function. When to use which
depends on the circumstances.



<h4>Using Arrays and Pass-By-Value</h4>

The one we generally prefer is <b>returning an <a
href="http://aolserver.com/doc/tcl8.3/TclCmd/array.htm#M8"><code>array
get</code></a>-formatted list</b>. It has all the nice properties of
pass-by-value, and it uses Tcl arrays, which have good native support.


<blockquote><pre>
ad_proc ad_get_user_info { user_id } {
    db_1row user_info { select first_names, last_name, email from users where user_id = :user_id }
    return [list \
        name "$first_names $last_name" \
	email $email \
	namelink "&lt;a href=\"/shared/community-member?user_id=[ns_urlencode $user_id]\"&gt;$first_names $last_name&lt;/a&gt;" \
	emaillink "&lt;a href=\"mailto:$email\"&gt;$email&lt;/a&gt;"]
}

array set user_info [ad_get_user_info $user_id]

doc_body_append "$user_info(namelink) ($user_info(emaillink))"
</pre></blockquote>

You could also have done this by using an array internally and using <code>array get</code>:

<blockquote><pre>
ad_proc ad_get_user_info { user_id } {
    db_1row user_info { select first_names, last_name, email from users where user_id = :user_id }
    set user_info(name) "$first_names $last_name"
    set user_info(email) $email
    set user_info(namelink) "&lt;a href=\"/shared/community-member?user_id=[ns_urlencode $user_id]\"&gt;$first_names $last_name&lt;/a&gt;"
    set user_info(emaillink) "&lt;a href=\"mailto:$email\"&gt;$email&lt;/a&gt;"
    return [array get user_info]
}
</pre></blockquote>




<h4>Using Arrays and Pass-By-Reference</h4>

Sometimes pass-by-value incurs too much overhead, and you'd rather
pass-by-reference.  Specifically, if you're writing a proc that uses
arrays internally to build up some value, there are many entries in
the array, and you're planning on iterating over the proc many
times. In this case, pass-by-value is expensive, and you'd use
pass-by-reference. 

<blockquote><em>The transformation of the array into a list and back to
an array takes, in our test environment, approximately 10 microseconds
per entry of 100 character's length. Thus you can process about 100
entries per milisecond. The time depends almost completely on the
number of entries, and almost not at all on the size of the
entries.</em></blockquote>

You implement pass-by-reference in Tcl by <b>taking the name of an
array as an argument and <code>upvar</code> it</b>.


<blockquote><pre>
ad_proc ad_get_user_info { 
    -array:required
    user_id 
} {
    upvar $array user_info
    db_1row user_info { select first_names, last_name, email from users where user_id = :user_id }
    set user_info(name) "$first_names $last_name"
    set user_info(email) $email
    set user_info(namelink) "&lt;a href=\"/shared/community-member?user_id=[ns_urlencode $user_id]\"&gt;$first_names $last_name&lt;/a&gt;"
    set user_info(emaillink) "&lt;a href=\"mailto:$email\"&gt;$email&lt;/a&gt;"
}

ad_get_user_info -array user_info $user_id

doc_body_append "$user_info(namelink) ($user_info(emaillink))"
</pre></blockquote>

<p>

We prefer pass-by-value over pass-by-reference. Pass-by-reference
makes the code harder to read and debug, because changing a value in
one place has side effects in other places. Especially if have a chain
of <code>upvar</code>s through several layers of the call stack,
you'll have a hard time debugging.




<h4>Multisets: Using <code>ns_set</code>s and Pass-By-Reference</h4>

An array is a type of <i>set</i>, which means you can't have multiple
entries with the same key. Data structures that can have multiple
entries for the same key are known as a <i>multiset</i> or <i>bag</i>.

<p>

If your data can have <b>multiple entries with the same key</b>, you
should use the AOLserver built-in <a
href="http://www.aolserver.com/doc/3.0/tcldev/tapi-120.htm#197598"><b><code>ns_set</code></b></a>.
You can also do a case-insensitive lookup on an <code>ns_set</code>,
something you can't easily do on an array. This is especially useful
for things like HTTP headers, which happen to have these exact
properties.

<p>

You always use pass-by-reference with <code>ns_set</code>s, since they
don't have any built-in way of generating and reconstructing
themselves from a string representation. Instead, you pass the handle
to the set.

<blockquote><pre>
ad_proc ad_get_user_info {
    -set:required
    user_id
} {
    db_1row user_info { select first_names, last_name, email from users where user_id = :user_id }
    ns_set put $set name "$first_names $last_name"
    ns_set put $set email $email
    ns_set put $set namelink "&lt;a href=\"/shared/community-member?user_id=[ns_urlencode $user_id]\"&gt;$first_names $last_name&lt;/a&gt;"
    ns_set put $set emaillink "&lt;a href=\"mailto:$email\"&gt;$email&lt;/a&gt;"
}

set user_info [ns_set create]
ad_get_user_info -set $user_info $user_id

doc_body_append "[ns_set get $user_info namelink] ([ns_set get $user_info emaillink])"
</pre></blockquote>

We don't recommend <code>ns_set</code> as a general mechanism for
passing sets (as opposed to multisets) of data. Not only do they
inherently use pass-by-reference, which we dis-like, they're also
somewhat clumsy to use, since Tcl doesn't have built-in syntactic
support for them.

<p>

Consider for example a loop over the entries in a <code>ns_set</code>
as compared to an array:

<blockquote><pre>
# ns_set variant
set size [ns_set size $myset]
for { set i 0 } { $i &lt; $size } { incr i } {
    puts "[ns_set key $myset $i] = [ns_set value $myset $i]"
}

# array variant
foreach name [array names myarray] {
    puts "$myarray($name) = $myarray($name)"
}
</pre></blockquote>

And this example of constructing a value:

<blockquote><pre>
# ns_set variant
set myset [ns_set create]
ns_set put $myset foo $foo
ns_set put $myset baz $baz
return $myset

# array variant
return [list
    foo $foo
    baz $baz
]
</pre></blockquote>

<code>ns_set</code>s are designed to be lightweight, so
memory consumption should not be a problem. However, when using
<code>ns_set get</code> to perform lookup by name, they perform a
linear lookup, whereas arrays use a hash table, so
<code>ns_set</code>s are slower than arrays when the number of
entries is large.


<hr>

<a
href="mailto:michael@arsdigita.com"><address>michael@arsdigita.com</address></a>
<a href="mailto:jsalz@mit.edu"><address>jsalz@mit.edu</address></a> <a
href="mailto:lars@pinds.com"><address>lars@pinds.com</address></a>


</body>

</html>
